apply plugin: 'scala'
apply plugin: 'forge'

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

version = "1.0.0"
group = "id2h"
archivesBaseName = "yatm"

minecraft {
    version = "1.7.10-10.13.4.1448-1.7.10"
    runDir = "run"

    replace '@VERSION@', project.version
    replace '@MC_VERSION@', version
}

// Because of some oddities in the AE api/dev I'll need these...
repositories {
    mavenLocal()

    maven {
        name "ChickenBones"
        url "http://chickenbones.net/maven/"
    }

    maven {
        name "Mobius"
        url "http://mobiusstrip.eu/maven"
    }

    maven {
        name "FireBall API Depot"
        url "http://dl.tsr.me/artifactory/libs-release-local"
    }

    maven {
        name = "Player"
        url = "http://maven.ic2.player.to/"
    }

    maven {
        name = "Tterrag"
        url = "http://maven.tterrag.com/"
    }

    maven  {
        name = "RX14 Proxy"
        url = "http://mvn.rx14.co.uk/repo/"
    }

    maven {
        name "OpenComputers Repo"
        url = "http://maven.cil.li/"
    }

    ivy {
        name "BuildCraft"
        artifactPattern "http://www.mod-buildcraft.com/releases/BuildCraft/[revision]/[module]-[revision]-[classifier].[ext]"
    }
}

dependencies {
    compile "appeng:appliedenergistics2:rv2-stable-10:dev"
}

sourceSets {
    main {
        scala {
            srcDir 'src/main'
        }
        resources {
            srcDir 'src/resources'
        }
    }
    api {
        java {
            srcDir 'src/api'
        }
    }
}

processResources
{
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        // ${version}   and    ${mcversion}    are the exact strings being replaced
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else, that's not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

// --------------------
// extra jar section
// -------------------

// add a source jar
task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

// add a javadoc jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

// because the normal output has been made to be obfuscated
task deobfJar(type: Jar) {
    from sourceSets.main.output
    from sourceSets.api.output
    classifier = 'dev'
}

// make sure all of these happen when we run build
build.dependsOn sourceJar, javadocJar, deobfJar
